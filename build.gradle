//file:noinspection GroovyAssignabilityCheck
//file:noinspection GroovyAccessibility
//file:noinspection GradlePackageVersionRange
plugins {
	id "fabric-loom" version "1.0.+" // Loom is the Gradle plugin that handles the remapping of Minecraft and Minecraft dependencies.
	id "io.github.juuxel.loom-quiltflower" version "1.+" // Quiltflower, a better decompiler
	id "maven-publish"
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

String buildNumber = System.getenv("GITHUB_RUN_NUMBER")
version = mod_version + (buildNumber != null ? "-${buildNumber}" : "") + "+${project.minecraft_version}"

group = project.maven_group
archivesBaseName = project.archives_base_name


repositories {
	maven { url = "https://maven.quiltmc.org/repository/release" }
	maven { url = "https://api.modrinth.com/maven" } // LazyDFU, Lithium
	maven { url = "https://maven.terraformersmc.com/releases/" } // Mod Menu, EMI
	maven { url = "https://maven.shedaniel.me/" } // Cloth Config, REI
	maven { url = "https://mvn.devos.one/snapshots/" } // Create, Porting Lib, Forge Tags, Milk Lib
	maven { url = "https://cursemaven.com" } // Forge Config API Port
	maven { url = "https://maven.tterrag.com/" } // Registrate and Flywheel
	maven { url = "https://maven.cafeteria.dev/releases" } // Fake Player API
	maven { url = "https://maven.jamieswhiteshirt.com/libs-release" } // Reach Entity Attributes
	maven { url = "https://jitpack.io/" } // Mixin Extras, fabric ASM
	maven { url = "https://dvs1.progwml6.com/files/maven/" } // JEI
	maven { url = "https://maven.parchmentmc.org" } // Parchment mappings
}

dependencies {
	implementation 'org.jetbrains:annotations:23.0.0'

	// dev environment
	minecraft("com.mojang:minecraft:${project.minecraft_version}")
	mappings(loom.layered {
		it.mappings("org.quiltmc:quilt-mappings:${project.minecraft_version}+build.${project.qm_version}:intermediary-v2")
		it.parchment("org.parchmentmc.data:parchment-${project.minecraft_version}:${project.parchment_version}@zip")
		it.officialMojangMappings { nameSyntheticMembers = false }
	})
	modImplementation("net.fabricmc:fabric-loader:${project.loader_version}")
	modImplementation("net.fabricmc.fabric-api:fabric-api:${project.fabric_version}")

	// mod dev dependencies
	modLocalRuntime("maven.modrinth:lazydfu:0.1.3")
	modLocalRuntime("maven.modrinth:lithium:mc1.19.2-0.10.0")
	modLocalRuntime("me.shedaniel.cloth:cloth-config-fabric:${project.cloth_config_version}") { exclude(group: "net.fabricmc.fabric-api") } // required for suggestion tweaker and REI
	modLocalRuntime("com.terraformersmc:modmenu:${project.modmenu_version}") { exclude group: "net.fabricmc"; exclude group: "net.fabricmc.fabric-api" } // always good to have

	// used to prevent forge config api port from complaining
	modImplementation("com.github.AlphaMode:fakeconfig:master-SNAPSHOT") { exclude(group: "net.fabricmc.fabric-api") }
	modImplementation("com.github.AlphaMode:fakeconfigtoml:master-SNAPSHOT") { exclude(group: "net.fabricmc.fabric-api") }

	// recipe viewers
	modRuntimeOnly("dev.architectury:architectury-fabric:${project.architectury_version}") // for REI
	modRuntimeOnly("me.shedaniel.cloth:basic-math:0.6.0") // for REI
	modCompileOnly("me.shedaniel:RoughlyEnoughItems-api-fabric:${project.rei_version}") { transitive = false }
	modCompileOnly("me.shedaniel:RoughlyEnoughItems-fabric:${project.rei_version}") { transitive = false }
	modCompileOnly("dev.emi:emi:${project.emi_version}") { transitive = false }
	modCompileOnly("mezz.jei:jei-${project.minecraft_version}-fabric:${project.jei_version}") { transitive = false }

	def recipeViewer = project.recipe_viewer
	if (recipeViewer == "emi") {
		modLocalRuntime("dev.emi:emi:${project.emi_version}")  { transitive = false }
	} else if (recipeViewer == "jei") {
		modLocalRuntime("mezz.jei:jei-${project.minecraft_version}-fabric:${project.jei_version}") { transitive = false }
	} else if (recipeViewer == "rei") {
		modLocalRuntime("me.shedaniel:RoughlyEnoughItems-fabric:${project.rei_version}") { transitive = false }
	} else {
		println("unknown recipe viewer: $recipeViewer")
	}

	// create setup
	modImplementation("com.simibubi.create:create-fabric-${project.minecraft_version}:${project.create_version}+${project.minecraft_version}") { transitive = false }
	modImplementation("io.github.fabricators_of_create.Porting-Lib:porting-lib:${project.port_lib_version}+${project.minecraft_version}")
	modImplementation("dev.cafeteria:fake-player-api:${project.fake_player_api_version}")
	modImplementation("com.electronwill.night-config:core:${project.night_config_core_version}")
	modImplementation("com.electronwill.night-config:toml:${project.night_config_toml_version}")
	modImplementation("curse.maven:forge-config-api-port-fabric-547434:${project.config_api_id}") { transitive = false }
	modImplementation("com.tterrag.registrate_fabric:Registrate:${project.registrate_version}")
	modImplementation("com.jozufozu.flywheel:flywheel-fabric-${flywheel_minecraft_version}:${project.flywheel_version}")
	modImplementation("com.jamieswhiteshirt:reach-entity-attributes:${project.reach_entity_attributes_version}")
	modImplementation("io.github.tropheusj:milk-lib:${project.milk_lib_version}")
	implementation("com.google.code.findbugs:jsr305:${project.jsr305_version}")
}

processResources {
	// require dependencies to be the version compiled against or newer
	Map<String, String> properties = new HashMap<>()
	properties.put("version", project.version)
	properties.put("loader_version", project.loader_version)
	properties.put("fabric_version", project.fabric_version)
	properties.put("create_version", project.create_version + "+" + project.minecraft_version)
	properties.put("minecraft_version", project.minecraft_version)
	properties.put("java_version", sourceCompatibility)

	properties.forEach((k, v) -> inputs.property(k, v))

	filesMatching("fabric.mod.json") {
		expand properties
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = Integer.parseInt(sourceCompatibility)
}

java {
	withSourcesJar()
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.archivesBaseName}" }
	}
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			artifactId = archivesBaseName + "-${project.minecraft_version}"
			from components.java
		}
	}

	// See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
	repositories {
		// Add repositories to publish to here.
		// Notice: This block does NOT have the same function as the block in the top level.
		// The repositories here will be used for publishing your artifact, not for
		// retrieving dependencies.
	}
}
